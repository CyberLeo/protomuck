#!/bin/csh -f
#
# The default ports that the MUCK server should listen to, if no port
# numbers were supplied as command line arguments to this script.
# To list multiple ports, separate them with spaces.  ie:
# PORTS="8801 8802 8803"
# Note: These ports do not override the ports in @tune.
set PORTS="2 $*"

#
# You'll want to edit this to match the base directory of your muck's files.
# This should be the directory containig the 'data' amd 'muf' directories.
#
set GAMEDIR = $HOME/proto/game

# The following are the paths to the db files to load, save to, and archive to.
# DBOLD is the path and filename of the the previous database archive.
# DBIN is the path and filename of the database to read in.
# DBOUT is the path and filename of the database that the muck should save to.
#
# On a successful restart, DBIN is moved to DBOLD, and DBOUT is moved to DBIN,
# then the server is started.  The server will save it's db to DBOUT.
#
set DBOLD = data/proto.old
set DBIN = data/proto.db
set DBOUT = data/proto.new


# It's doubtful you will want to change this, unless you compile a different
# path and filename into the server.  This is the file that deltadumps are
# saved to.  After a successful restart, these deltas will be appended to the
# end of the DBIN file.
#
set DELTAS = data/deltas-file

###############################################################################
#
# You probably won't need to edit anything after this line.
#
###############################################################################

#limit stacksize 16384
limit coredumpsize 50000
limit memoryuse 16000

cd $GAMEDIR

#This is just so you get a new line if started by proto with a &.
echo 

if (! -x protomuck) then
	echo "You need to compile the protomuck server before you can run it."
	echo "Read proto/INSTALL to see what to do."
	exit 0
endif

echo "Restarting at: `date`"

umask 077

set muck = `ps x | grep protomuck | wc -l`
set datestamp = `date +%m%d%y%H%M`

if ($muck > 1) then
	echo "A protomuck is already running:"
	echo  `ps x | grep protomuck | grep -v grep`
	exit 0
endif

# Save any corefile:
set timestamp = "`date +'%y.%m.%d.%H.%M'`"
if (-r $GAMEDIR/core) then
    mv $GAMEDIR/core $GAMEDIR/core.$timestamp
endif

# Rename+compress "status" and "programs" logs too:
if (-r $GAMEDIR/logs/status) then
    mv $GAMEDIR/logs/status $GAMEDIR/logs/oldlogs/status.$timestamp
    gzip -2 $GAMEDIR/logs/oldlogs/status.$timestamp
endif

if (-r $GAMEDIR/logs/programs) then
    mv $GAMEDIR/logs/programs $GAMEDIR/logs/oldlogs/programs.$timestamp
    gzip -2 $GAMEDIR/logs/oldlogs/programs.$timestamp
endif

touch logs/restarts
echo "`date` - `who am i`" >> logs/restarts

if (-r $DBOUT.PANIC) then
	set end = "`tail -1 $DBOUT.PANIC`"
	if ("$end" == "***END OF DUMP***" ) then
		mv $DBOUT.PANIC $DBOUT
		rm -f $DELTAS
	else
		rm $DBOUT.PANIC
		echo "Warning: PANIC dump failed on "`date` | mail `whoami`
	endif
endif

#
# Fix the next lines to save all old dbs with datestamps if desired.
#

rm -f $DBOLD $DBOLD.gz
if (-r $DBOUT) then
	( protomuck -convert -decompress $DBIN $DBOLD ; gzip -8 $DBOLD ) &
#	mv $DBOLD $DBOLD.$datestamp
#	nice -20 gzip -9 $DBOLD.$datestamp &
	mv -f $DBOUT $DBIN
endif

if (! -r $DBIN) then
	echo "Hey\!  You gotta have a "$DBIN" file to restart the server\!"
	echo "Restart attempt aborted."
	exit
endif

set end = "`tail -1 $DBIN`"
if ("$end" != '***END OF DUMP***' ) then
	echo "WARNING\!  The "$DBIN" file is incomplete and therefore corrupt\!"
	echo "Restart attempt aborted."
	exit
endif

if (-r $DELTAS) then
	echo "Restoring from delta."
	set end = "`tail -1 $DELTAS`"
	if ("$end" == "***END OF DUMP***" ) then
		cat $DELTAS >> $DBIN
	else
		echo "Last delta is incomplete.  Truncating to previous dump."
		grep -n '^***END OF DUMP***' $DELTAS|tail -1 >! .ftmp$$
		set llinum = "`cut -d: -f1 < .ftmp$$`"
		set llcnt = "`wc -l < .ftmp$$`"
		if ($llcnt > 0) then
			head -$llinum $DELTAS >> $DBIN
		else
			echo "Hmm.  No previous delta dump."
		endif
		rm .ftmp$$
	endif
	rm -f $DELTAS
endif

set dbsiz=`ls -1s $DBIN | awk '{print $1}'`
set diskfree=`df -k . | tail -1 | awk '{print $4}'`

@ diskneeded = $dbsiz * 3
@ spacediff = $diskneeded - $diskfree

if ($diskfree < $diskneeded) then
    echo "WARNING: you have insufficient disk space."
    echo "The server is starting anyways, but you should immediately clear out old log"
    echo "files, etc. to free up approximately $spacediff more K of disk space."
endif

# echo "Server started at: `date`"
( ./protomuck $DBIN $DBOUT $PORTS >& logs/ttyerr.log; ./coremail ) &
# echo "Server stopped at: `date`"

