dnl
dnl Process this file with autoconf to produce a configure script.
dnl
dnl TinyMUCK fb5.x auto-config script writen by: Peter "WhiteFire" Torkelson
dnl
AC_REVISION($Revision: 1.7 $)dnl
AC_INIT(game.c)
AC_CONFIG_HEADER(inc/autoconf.h)
echo " "
echo "ProtoMUCK auto-configure script."
echo " "
echo "This script will try and determine things about your system so"
echo "that Proto can compile correctly. This will create your Makefile"
echo "and the header file autoconf.h in the include directory."
echo " "

dnl
dnl Find the compiler first.
dnl
AC_PROG_CC
AC_PROG_CPP

dnl
dnl Specific systems tests.
dnl
AC_ISC_POSIX
AC_MINIX

dnl AC_PROG_LEX
dnl AC_PROG_YACC

AC_ARG_WITH(ssl,
[  --with-ssl[=ssltop_path]  enable ssl encryption and authorization],
[
if test "$withval" = yes; then
        SSLTOP=
else
        SSLTOP=$withval
fi

found_sslh="0"
if test "$SSLTOP" = ""; then
        AC_CHECK_HEADERS(ssl.h ssl/ssl.h openssl/ssl.h,found_sslh="1")
else
        oldinc=$INC
        INC="-I$SSLTOP $oldinc"
        AC_CHECK_HEADERS($SSLTOP/ssl.h,
                AC_DEFINE(HAVE_SSL_H)
                found_sslh="1"
        )
        if test "$found_sslh" = "0"; then
                INC="-I$SSLTOP/include $oldinc"
                AC_CHECK_HEADERS($SSLTOP/include/ssl.h,
                        AC_DEFINE(HAVE_SSL_H)
                        found_sslh="1"
                )
        fi
        if test "$found_sslh" = "0"; then
                INC="-I$SSLTOP/include $oldinc"
                AC_CHECK_HEADERS($SSLTOP/include/ssl/ssl.h,
                        AC_DEFINE(HAVE_SSL_SSL_H)
                        found_sslh="1"
                )
        fi
        if test "$found_sslh" = "0"; then
                INC="-I$SSLTOP/include $oldinc"
                AC_CHECK_HEADERS($SSLTOP/include/openssl/ssl.h,
                        AC_DEFINE(HAVE_OPENSSL_SSL_H)
                        found_sslh="1"
                )
        fi
fi
if test "$found_sslh" = "0"; then
        AC_MSG_ERROR([cannot locate ssl.h header file.])
fi

found_ssl_lib="0"
if test "$SSLTOP" = ""; then
        AC_TRY_LINK_FUNC(SSL_accept, found_ssl_lib="1")
        AC_CHECK_LIB(ssl, SSL_accept,
                LIBS="-lssl -lcrypto $LIBS"
                found_ssl_lib="1"
        )
else
        LIBS="-L$SSLTOP/lib -lssl -lcrypto $LIBS"
        AC_CHECK_LIB(ssl, SSL_accept, found_ssl_lib="1")
fi
if test "$found_ssl_lib" = "0"; then
        AC_MSG_ERROR([cannot locate libssl library.])
fi

AC_DEFINE(USE_SSL)
AC_SUBST(DEFS)
AC_SUBST(INC)
AC_SUBST(SSLTOP)
])

AC_ARG_WITH(mysql,
[  --mysql[=mysqltop_path]   use MySQL database support],
[
if test "$withval" = yes; then
    MYSQLTOP=
else
    MYSQLTOP=$withval
fi

found_mysql="0"
if test "$MYSQLTOP" = ""; then
    AC_CHECK_HEADERS(mysql.h,
	 found_mysql="1")

    AC_CHECK_HEADERS(mysql/mysql.h,
        AC_DEFINE(HAVE_MYSQL_H)
        INCL="-Imysql -L/usr/lib/mysql $INCL" 
        found_mysql="1"
    )
else
    AC_CHECK_HEADERS($MYSQLTOP/mysql.h,  
        AC_DEFINE(HAVE_MYSQL_H)
        INCL="-I$MYSQLTOP -L$MYSQLTOP -L/usr/lib/mysql $INCL"
        found_mysql="1"
    )
fi

if test "$found_mysqlh" = "0"; then
    AC_MSG_ERROR([cannot locate mysql.h header file.])
fi

AC_DEFINE(SQL_SUPPORT)
AC_SUBST(INCL)
LIBS="-lmysqlclient $LIBS"
])

AC_ARG_ENABLE(asroot,
[  --enable-asroot         allow file primitives under root user],
[
if test "$enableval" != "no"; then
    AC_DEFINE(PROTO_AS_ROOT)
fi
])

AC_ARG_ENABLE(compress,
[  --disable-compress      disable database compression],
[
if test "$enableval" = "no"; then
    AC_DEFINE(NO_COMPRESS)
fi
])

dnl
dnl Header files
dnl
AC_CHECK_HEADERS(malloc.h memory.h string.h unistd.h sys/resource.h sys/signal.h)
AC_CHECK_HEADERS(sys/time.h sys/malloc.h)
AC_HEADER_STDC
AC_HEADER_DIRENT
AC_HEADER_TIME
echo "checking for tm_gmtoff"
AC_EGREP_HEADER([^_a-zA-Z]tm_gmtoff;, time.h, AC_DEFINE(HAVE_TM_GMTOFF))
AC_EGREP_HEADER([^_a-zA-Z]tm_gmtoff;, sys/time.h, AC_DEFINE(HAVE_SYS_TM_GMTOFF))

dnl
dnl Libraries and functions.
dnl
AC_HAVE_LIBRARY(m, [LIBS="$LIBS -lm"])
AC_HAVE_LIBRARY(socket,[LIBS="$LIBS -lsocket"] )
AC_HAVE_LIBRARY(nsl, [LIBS="$LIBS -lnsl"])
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(mallinfo getrlimit getrusage random)

dnl
dnl Types and structures
dnl
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_TYPE_SIGNAL
AC_STRUCT_TIMEZONE

dnl
dnl Compiler characteristics
dnl
AC_C_CONST
AC_C_INLINE

dnl
dnl Uname -a, just becuse.
dnl
echo "checking value of uname -a"
AC_DEFINE_UNQUOTED(UNAME_VALUE, "`uname -a`")

dnl
dnl And in the end, there was no more.
dnl
AC_OUTPUT(Makefile)
echo " "
echo "You should review the options in inc/config.h, and"
echo "then type the following to build your system:"
echo " "
echo " make"
echo " "

